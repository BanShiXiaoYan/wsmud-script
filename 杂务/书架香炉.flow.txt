//~silent
($localVer) = 20211124001
($changeLog) = 🌟 皇城搜索书架香炉，可使用长生恢复内力、慈航加速搜索。
($flowName) = 书架香炉
($flowCode) = sjxl
($flowPath) = 杂务
($flowFinder) = 根文件夹
($repourl) = https://cdn.jsdelivr.net/gh/mapleobserver/wsmud-script
@js ($version) = $.ajax({type: "GET", url: "(repourl)/version.txt", async: false, cache: false}).responseText;
[if] (version) != null
  @js ($remoteVer) = var v = (version);v.(flowCode)
[if] (localVer) < (remoteVer) && (remoteVer) != null
  [if] (UPnum) == null
    ($UPnum) = 0
  [if] (UPnum) < 2
    [if] (UPnum) == 1
      @print 未获取到最新版本，使用备用地址再次尝试...
      ($repourl) = http://wsmud-cdn.if404.com
    ($UPnum) = (UPnum) + 1
    [if] (flowPath) != 根文件夹 && (flowPath) != null
      ($flowSource) = (repourl)/(flowPath)/(flowName).flow.txt
    [else]
      ($flowSource) = (repourl)/(flowName).flow.txt
    [if] (flowFinder) == null
      ($flowFinder) = 根文件夹
    @js WG.SendCmd("tm 当前(flowName)版本【(localVer)】，将开始自动更新至最新【(remoteVer)】。")
    ($f_ss)={"name":"(flowName)","source":"(flowSource)","finder":"(flowFinder)"}
    @js var time = Date.parse( new Date());var f=(f_ss);var n=f["name"];var s=f["source"];var fd=f["finder"];WorkflowConfig.removeWorkflow({"name":n,"type":"flow","finder":fd});$.get(s,{stamp:time},function(data,status){WorkflowConfig.createWorkflow(n,data,fd);});
    @wait 1500
    ($log) = ✅(flowName)已更新至最新版【(remoteVer)】，即将自动运行。
  [else]
    ($log) = ⭕(flowName)暂时无法正常更新至【(remoteVer)】，直接运行当前版本【(localVer)】。
    ($UPnum) = null
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>
  [if] (UPnum) != null
    @js var f=(f_ss);ManagedPerformerCenter.start(f["name"], FlowStore.get(f["name"]));
    [exit]
[else]
  ($UPnum) = null
  [if] (remoteVer) == null
    ($log) = ⚠️获取远程版本失败，维持当前版本不变。
  [else]
    ($log) = 🚀开始运行(flowName)，当前版本【(remoteVer)】。更新内容：(changeLog)
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>

//检测状态
@js ($fbroom) = '(:room)'.indexOf('副本区域')
[if] (fbroom) != -1 || (:state)==推演 || (:state)==领悟
  ($alert_start) = 正在(:state)，停止流程！
  [(fbroom) != -1]($alert_start) = 正在副本中，停止流程！
  @js WG.SendCmd("tm (alert_start)")
  @print <ord>(alert_start)</ord>
  [exit]

[if] (HCplan) == null
  ($HCplan) = 书架和香炉
[if] (HCcycle) == null
  ($HCcycle) = 是
[if] (HCchangsheng) == null
  ($HCchangsheng) = 是
[if] (HCcihang) == null
  ($HCcihang) = 不使用
[if] (HCposui) == null
  ($HCposui) = 1
@cmdDelay 350
#select ($HCplan) = 请确保自己能自由进出书房<br/>&nbsp*&nbsp搜索方案,书架和香炉|只书架|只香炉,(HCplan)
#select ($HCcycle) = 重复尝试直到搜到,是|否,(HCcycle)
#select ($HCchangsheng) = 是否用长生诀回蓝,是|否,(HCchangsheng)
#select ($HCcihang) = 使用剑心加速搜索,不使用|慈航剑典|自创内功,(HCcihang)
#input ($HCposui) = 需要将皇城-广场设置为传送点<br/>&nbsp*&nbsp皇城-广场在破碎虚空的位置,(HCposui)
#config

[if] (HCchangsheng) == 是
  ($cs) = true
[else if] (HCchangsheng) == 否
  ($cs) = false
[if] (HCcihang) == 慈航剑典
  ($force) = cihangjiandian
[else if] (HCcihang) == 自创内功
  ($force) = (:id)
[if] (HCplan) == 书架和香炉
  ($sj) = true
  ($xl) = true
[if] (HCplan) == 只书架
  ($sj) = true
  ($xl) = false
[if] (HCplan) == 只香炉
  ($sj) = false
  ($xl) = true

($force0) = (:kf_nei)

<---
[if] (:mpPer) <= 0.2
  [if] (cs)
    ($force1) = (:kf_nei)
    enable force changshengjue
    @cd force.zhen
    @perform force.zhen
    @until (:cd force.zhen)==true
    enable force (force1)
  [else]
    @renew
    [continue]
--->
stopstate
[if] (force) != null
  enable force (force)
[while] (sj) || (xl)
  [if] (force) != null && (:status force)==false
    @cd force.xin
    @perform force.xin
    @tip 这里($cant)战斗|你晋入($jianxin)的无上道境
    [if] (cant) != null
      ($cant) = null
      jh fam 0 start
      @await 1000
      @perform force.xin
  jh fam 0 start;psxk (HCposui)
  [if] (sj) && (xl)
    go west;go north;lu ding;
    @tip 这里没啥可以看的了|除了木雕就是大型的铜鼎|找到($zdxl)一|深吸一口($zdxl)冒出的缕缕青烟
    @await 500
    go east;go east;lu sj;
    @tip 这里没啥可以看的了|似乎被人捷足先登|找到($zdsj)一排|没找到什么($zdsj)顺走的东西
  [if] (xl) && !(sj)
    go west;go north;lu ding;
    @tip 这里没啥可以看的了|除了木雕就是大型的铜鼎|找到($zdxl)一|深吸一口($zdxl)冒出的缕缕青烟
  [if] (sj) && !(xl)
    go north;go east;lu sj;
    @tip 这里没啥可以看的了|似乎被人捷足先登|找到($zdsj)一排|没找到什么($zdsj)顺走的东西
  [if] (zdsj) != null
    [if] "{b书架g}?" != "null"
      use {b书架}?
      @tip 书架已经摆放在你的卧室，目前可以存放($sjNum)本书籍|你家里的($sjFull)已经够大了
      [if] (sjNum) == 200 || (sjFull) != null
        ($HCplan) = 只香炉
        @print <hiy>书架已到达最大上限，搜索方案改为只搜香炉。</hiy>
    ($sj) = false
  [if] (zdxl) != null
    ($xl) = false
  @await 500
  [if] (:status force)==true
    [if] (sj) || (xl)
      [continue]
  @await 500
  $zdwk
  @wait 5000
  [if] (cs) = true
    [if] (HCcihang)!= 不使用 ||(:mpPer) <= 0.5
        @cd force.zhen
        ($force1) = (:kf_nei)
        stopstate;enable force changshengjue
        @perform force.zhen
        @until (:cd force.zhen)==true
        enable force (force1)
        $zdwk
  [if] !(sj) && !(xl)
    stopstate;enable force (force0)
    $zdwk
    [exit]
  [if] (HCcycle) == 否
    stopstate;enable force (force0)
    $zdwk
    [exit]
  [if] (force) != null
    @cd force.xin
  [else]
    @print 等待20秒继续尝试。
    @await 20000
  stopstate