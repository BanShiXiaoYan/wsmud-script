//~silent
// 查询Boss
// 谣言更新或任意频道（房间除外）发送 b 即可出现相关信息
// 点击 参数设置 按钮可设置Boss类型
($changeLog) = 增加自动寻路打Boss功能。
($localVer) = 2021112401
[if] (BossModel) == null
  ($BossModel) = 自动跳过天僧
($bossSet) = (BossModel)
[if] (BossTeam) == null
  ($BossTeam) = 关闭
[if] (BossTeam) == 开启
  ($bossSet) = (bossSet)；只打组队本
[if] (BossPost) == null
  ($BossPost) = 游戏信息区
[if] (BossPost) == 游戏信息区
  ($comClass) = content-message
  ($magPost) = 1
[else] (BossPost) == 插件信息区
  ($comClass) = WG_log
  ($magPost) = 0
[if] (BossDay) == null
  ($BossDay) = 1234567
($bossSet) = (bossSet)；每周激活【自动跳过】功能的天数：(BossDay)
[if] (BossAlert) == null
  ($BossAlert) = 关闭
($bossSet) = (bossSet)；Boss刷新发声提醒：(BossAlert)

[if] (:state) == 闭关
  ($action) = $zdwk
[else if] (:state) == 练习
  ($action) = $to 住房-练功房;$wait 1000;dazuo
[if] (content)==boss || (content)==b
  [if] (name)==(:name)
    ($mycall) = true
    @toolbar jh
    @toolbar tasks
    @await 350
    @task 挑战武神BOSS($num)次|今日可领取
    ($remain)=⚔️ 本周还可挑战(num)次
    // 本周已打完Boss
    [if] (num)==null
      ($BossInfo) = null
      ($log) = ✅ 本周已打完5次Boss\n⚙️ 当前设置：(bossSet)
      @js messageAppend("(log)",2,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)));$('.(comClass)')[0].scrollTop = 99999;
      //按钮命令
      ($finish) = true
    [else]
      // 获取已存在的Boss数据
      [if] (BossInfo) != null
        @js ($info)=("\""+"(BossInfo)"+"\"").replace(/\|/g,"\"|\"").split("|")
        @js ($i_date)=[(info)][0]
        @js ($i_time)=[(info)][1]
        @js ($bossName)=[(info)][2]
        @js ($bossPlace)=[(info)][3]
      [else]
        ($i_date) = null
      // 去客栈获取Boss信息
      [if] (i_date) != (:date) || (bossName) == null || (bossPlace) == null
        ($i_time)=null
        [if] (i_date) == notime
          ($log) = 🔍 已跳过或打过上一个Boss，前往客栈获取最新信息……
        [else]
          ($log) = 🔎 无今日数据，前往客栈获取……
        @js messageAppend("(log)",1,(magPost));
        [if] (:state) == 推演 || (:state) == 领悟 || (:room 副本区域,忧愁谷) == true
          [if] (:state) == 推演 || (:state) == 领悟
            ($log) = ⚠️ (:state)中，暂停前往客栈获取信息。
          [(:room 副本区域,忧愁谷) == true]($log) = ⚠️ 副本中，暂停前往客栈获取信息。
          @js messageAppend("<ord>(log)</ord>",1,(magPost));$('.(comClass)')[0].scrollTop = 99999;
          [exit]
        stopstate
        [while] (:room) != 扬州城-有间客栈
          $to 扬州城-有间客栈
          @await 500
        //dating {r店小二}?
        give {r店小二}? 20000 money
        @tip 这位客官，我最近听不少人说是在($bossPlace)见到过($bossName)。|这两天也($notime)什么消息|小的听到的消息($all)您老了
      [if] (notime) != null || (all) != null
        [if] (notime) != null
          ($log) = ⏳ Boss尚未刷新，请稍后再试\n⚙️ 当前设置：(bossSet)\n(remain)</ord>
        [else if] (all) != null
          ($log) = <hig>✅ 本周已打完5次Boss\n⚙️ 当前设置：(bossSet)</hig>
        @js messageAppend("(log)",1,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)));
        [if] (action) != null && (:state) != 闭关 && (:state) != 练习
          (action)
  [else]
    [exit]
// 谣言获取Boss
[else if] (channel)==谣言
  @js ($bossName) = "(content)".match("听说([^%]+)出现在")[1]
  @js ($bossPlace) = "(content)".match("出现在([^%]+)一带。")[1]
  @toolbar jh
  @toolbar tasks
  @await 350
  @task 挑战武神BOSS($num)次|今日可领取
  ($remain)=⚔️ 本周还可挑战(num)次
// 处理参数
[if] (bossName) == 战神蚩尤
  ($bossName) = 蚩尤
[if] (bossPlace) != null && (bossName) != null
  [if] (i_time) == null
    ($BossInfo)=(:date)|(:hour):(:minute):(:second)|(bossName)|(bossPlace)
  ($fbdata) = "树林":"cr yz/lw/shangu 0","财主家":"cr yz/cuifu/caizhu 1","丽春院":"cr yz/lcy/dating 0","流氓巷":"cr yz/lmw/xiangzi1 0","兵营":"cr yz/by/damen 0","庄府":"cr bj/zhuang/xiaolu 0","鳌拜府":"cr bj/ao/damen 0","天地会":"cr bj/tdh/hct 0","神龙教":"cr bj/shenlong/haitan 0","关外":"cr bj/guanwai/damen 0","温府":"cr cd/wen/damen 0","五毒教":"cr cd/wudu/damen 0","恒山":"cr wuyue/hengshan/daziling 0","青城山":"cr wuyue/qingcheng/shanlu 0","衡山":"cr wuyue/henshan/hengyang 0","泰山":"cr wuyue/taishan/daizong 0","嵩山":"cr wuyue/songshan/taishi 0","云梦沼泽":"cr cd/yunmeng/senlin 0","桃花岛":"cr taohua/haitan 1","白驼山":"cr baituo/damen 0","星宿海":"cr xingxiu/xxh6 0","冰火岛":"cr mj/bhd/haibian 1","移花宫":"cr huashan/yihua/shandao 1","燕子坞":"cr murong/anbian 1","黑木崖":"cr heimuya/shangu 0","缥缈峰":"cr lingjiu/shanjiao 1","光明顶":"cr mj/shanmen 0","天龙寺":"cr tianlong/damen 1","血刀门":"cr xuedao/shankou 0","古墓派":"cr gumu/gumukou 1","华山论剑":"cr huashan/lunjian/leitaixia 0"
  @js ($fbcr) = var fb={(fbdata)};fb.(bossPlace)
  [if] (bossPlace)==流氓巷 || (bossPlace)==神龙教 || (bossPlace)==五毒教 || (bossPlace)==云梦沼泽 || (bossPlace)==白驼山 || (bossPlace)==冰火岛 || (bossPlace)==移花宫 || (bossPlace)==黑木崖 || (bossPlace)==光明顶 || (bossPlace)==血刀门
    ($team) = true
// 生成快捷按钮
[if] (fbcr) != null
  [if] (:state) == 推演 || (:state) == 领悟
    ($log) = <ord>⚠️ (:state)中，请注意操作！</ord>
    @js messageAppend("(log)",1,(magPost));
  [if] (:state) == 闭关 || (:state) == 练习 || (:room) == 扬州城-有间客栈
    [if] (bossName) != 浪翻云 && (BossModel) == 只打浪翻云
      ($model) = true
    [else if] (bossName) != 烛九阴 && (BossModel) == 只打烛九阴
      ($model) = true
    [else if] (bossName) != 蚩尤 && (BossModel) == 只打蚩尤
      ($model) = true
    [else if] (bossName) != 庞斑 && (BossModel) == 只打庞斑
      ($model) = true
    [else if] (bossName) != 浪翻云 && (bossName) != 烛九阴 && (BossModel) == 只打浪翻云烛九阴
      ($model) = true
    [else if] (bossName) != 浪翻云 && (bossName) != 烛九阴 && (bossName) != 蚩尤 && (BossModel) == 跳过天僧庞斑
      ($model) = true
    [else if] (bossName) == 天僧 && (BossModel) == 自动跳过天僧
      ($model) = true
    [if] (BossTeam) == 开启 && (team) != true
      ($model) = true
  [if] (model) == true
    // 根据每周日期设定，判断是否激活【自动跳过】功能
    [if] (:day) == 0
      ($today) = 7
    [else]
      ($today) = (:day)
    @js ($match) = '(BossDay)'.indexOf('(today)')
    [if] (match) < 0
      ($log) = <hiy>📆 根据每周设定，今日不会【自动跳过Boss】。</hiy>\n⚙️ 当前设置：(bossSet)
      @js messageAppend("(log)",2,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)));$('.(comClass)')[0].scrollTop = 99999;
      ($jump) = true
    // 执行自动跳过操作
    [else]
      ($log) = <ord>💎 (BossInfo)\n<hiy>⚠️ 根据设置<ord>【(bossSet)】</ord>，自动跳过当前Boss(bossName)</hiy>\n(remain)</ord>
      @js messageAppend("(log)",1,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)));
      team out;stopstate;(fbcr) 0;cr over;$wait 2000;(action)
      ($BossInfo)=notime
      ($jump) = true
  [else]
    [if] (team) == true
      @js ($fbcr2) = var f="(fbcr)";f=f.substr(0,f.length-1)+"2"
      @js messageAppend("<ord>💎 (BossInfo)\n⚙️ 当前设置：(bossSet)\n(remain)</ord>",1,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-auto">🍉 <hiy>自动打Boss</hiy></span>`),$(`<span class="redboss boss-single">🥕 进入 <hic>(bossPlace)</hic> => <ord>寻找Boss</ord></span>`),$(`<span class="redboss boss-team">🚌 进入 <hic>(bossPlace)(<hiy>组队</hiy>)</hic> => <ord>寻找Boss</ord></span>`),$(`<span class="redboss boss-skip">🥚 进出 <hic>(bossPlace)</hic> => <hig>刷新Boss</hig></span>`),$(`<span class="redboss boss-refresh">🙉 前往客栈刷新</span>`),$(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)))
    [else]
      @js messageAppend("<ord>💎 (BossInfo)\n⚙️ 当前设置：(bossSet)\n(remain)</ord>",1,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss boss-auto">🍉 <hiy>自动打Boss</hiy></span>`),$(`<span class="redboss boss-single">🥕 进入 <hic>(bossPlace)</hic> => <ord>寻找Boss</ord></span>`),$(`<span class="boss-skip">🥚 进出 <hic>(bossPlace)</hic> => <hig>刷新Boss</hig></span>`),$(`<span class="redboss boss-refresh">🙉 前往客栈刷新</span>`),$(`<span class="redboss boss-setting">⚙️ 设置参数</span>`)))
    [if] (channel) == 谣言
      [if] (BossAlert) == 提示音
        @beep
      [else if] (BossAlert) == 语音播报
        $tts (:name)，(bossName)出现在(bossPlace)。
      [else if] (BossAlert) == 远程通知
        @push 【Boss刷新】(:name)：(bossName)出现在(bossPlace)。
    [if] (action) != null && (:state) != 闭关 && (:state) != 练习
      (action)
//按钮命令
[if] (mycall) == true || (channel) == 谣言 || (finish) == true || (jump) = true
  @js $(".redboss").off("click")
  @js $(".boss-auto").on("click", function(){ToRaid.perform(`//~silent\n@call 自动红boss (bossName),(bossPlace)`);})
  @js $(".boss-single").on("click", function(){ToRaid.perform(`//~silent\nstopstate;(fbcr) 0\n($BossInfo)=notime\n@js messageAppend("🥕 进入单人副本 <hic>(bossPlace)</hic> => <ord>(bossName)</ord>",2,(magPost));$('.(comClass)')[0].scrollTop = 99999;`);})
  @js $(".boss-team").on("click", function(){ToRaid.perform(`//~silent\nstopstate;(fbcr2) 0\n($BossInfo)=notime\n@js messageAppend("🚌 进入组队副本 <hic>(bossPlace)</hic> => <ord>(bossName)</ord>",2,(magPost));$('.(comClass)')[0].scrollTop = 99999;`);})
  @js $(".boss-skip").on("click", function(){ToRaid.perform(`//~silent\nteam out;stopstate;(fbcr) 0;cr over;$wait 1000;(action)\n($BossInfo)=notime\n@js messageAppend("🥚 已跳过<ord>(bossName)</ord>！",1,(magPost));$('.(comClass)')[0].scrollTop = 99999;`);})
  @js $(".boss-refresh").on("click", function(){ToRaid.perform(`($BossInfo)=null\ntm b`);})
  @js $(".boss-setting").on("click", function(){ToRaid.perform(`//~silent\n#select ($BossModel)=Boss类型,我全都要|自动跳过天僧|跳过天僧庞斑|只打浪翻云烛九阴|只打浪翻云|只打烛九阴|只打蚩尤|只打庞斑,(BossModel)\n#select ($BossTeam)=只打组队本,关闭|开启,(BossTeam)\n#select ($BossPost)=Boss信息显示位置,游戏信息区|插件信息区,(BossPost)\n#input ($BossDay)=每周哪几天激活自动跳过Boss功能（默认1234567）<br/>&nbsp*&nbsp<hiz>周一为1，以此类推周日为7</hiz>,(BossDay)\n#select ($BossAlert)=Boss刷新时提醒（远程通知需在plugins设置中先开启）,关闭|提示音|语音播报|远程通知,(BossAlert)\n#select ($BossTanlong)=找到Boss后是否使用探龙叫杀,原地等待通知玩家|直接叫杀|自定义命令|剑心+融合探龙|剑心+正常探龙|融合探龙|正常探龙,(BossTanlong)\n#input ($BossBefore)=进副本前后正常装备技能触发,(BossBefore)\n#input ($BossSkill)=杀Boss前换装备技能触发(建议用自命令调用),(BossSkill)\n#select ($BossWarning)=遭遇问题时发出警报(找不到Boss、被打死2次等),不提示|弹窗提醒|提示音|语音播报|远程通知,(BossWarning)\n#config\ntm b`);})
  //检查版本
  //($repourl) = https://cdn.jsdelivr.net/gh/mapleobserver/wsmud-script
  ($repourl) = http://wsmud-cdn.if404.com
  @js ($version) = $.ajax({type: "GET", url: "(repourl)/version.txt", async: false, cache: false}).responseText;
  [if] (version) != null
    @js ($remoteVer) = var v = (version);v.rboss
  @js ($rVer) = [(remoteVer)][0]
  [if] (localVer) < (rVer) && (remoteVer) != null
    @js ($newT) = [(remoteVer)][1]
    @js messageAppend("<ord>检测到新版本！(localVer) => (rVer)</ord>",1,(magPost));$('.(comClass) pre').append($(`<div class="item-commands"></div>`).append($(`<span class="redboss update">♻️ 更新 => <hig>红boss报告</hig></span>`)))
    @js $(".update").on("click", function(){ToRaid.perform(`//~silent\n@js unsafeWindow.TriggerCenter.remove("红boss报告")\n@await 350\n@js Server.importTrigger("(newT)")\n@await 1000\ntm b`);})
  // 检查自动红boss流程是否存在
  @js ($flowGet) = let s=FlowStore.get('自动红boss');s==null?1:0
  [if] (flowGet)==1
    @print <hig>【自动红boss】流程不存在，自动导入。</hig>
    @js var time = Date.parse( new Date());$.get('(repourl)/自动红boss.flow.txt',{stamp:time},function(data,status){WorkflowConfig.createWorkflow('自动红boss',data,'根文件夹');});
  [if] (BossTanlong) == null || (BossSkill) == null || (BossWarning) == null
    [(BossTanlong) == null]($BossTanlong) = 剑心+融合探龙
    #select ($BossTanlong) = <ord>初次使用，请先设置自动打Boss相关参数</ord><br/>&nbsp*&nbsp找到Boss后是否使用探龙叫杀,原地等待通知玩家|直接叫杀|自定义命令|剑心+融合探龙|剑心+正常探龙|融合探龙|正常探龙,(BossTanlong)
    #input ($BossCustomKill) = 叫杀Boss自定义命令（上个选项需选自定义命令才有效，多个命令用英文分号隔开，不要用@开头的命令）,(BossCustomKill)
    [(BossBefore) == null]($BossBefore) = $usezml 正常
    #input ($BossBefore) = 进副本前后正常装备技能触发,(BossBefore)
    [(BossSkill) == null]($BossSkill) = enable force none;$usezml 血剑
    #input ($BossSkill) = 杀Boss前换装备技能触发(建议用自命令调用),(BossSkill)
    [(BossWarning) == null]($BossWarning) = 语音播报
    #select ($BossWarning) = 遭遇问题时发出警报(找不到Boss、被打死2次等),不提示|弹窗提醒|提示音|语音播报|远程通知,(BossWarning)
    #config
  @js $('.(comClass)')[0].scrollTop = 99999;