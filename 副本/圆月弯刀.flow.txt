//~silent
// 对四区沧浪、四区白三三两位大佬的圆月弯刀流程进行了整合
// 四区-黑白郎君&&沧浪&&白三三
// 2022-02-05 16:15
// 1.7 优化离开副本的过程，避免命令过多卡地图。
// 1.8 修改正常结婚和超龄结婚的差异操作，减少可能存在的bug。
// 1.9 优化拿草的后续判断；进地窖后不选择直接叫杀避免小楼面板变高。
// 1.10 优化进地窖的方式，避免出招过多导致卡住没进去。
// 1.11 优化选项说明文字。
// 10.7 优化10分钟等待过程。
($localVer) = 2022020501
($changeLog) = 🆕 进地窖砍完小楼后总次数会清零重置；拿橙刀后终止流程避免浪费精力。
($flowName) = 圆月弯刀
($flowCode) = yywd
($flowPath) = 副本
($flowFinder) = 根文件夹
($repourl) = https://cdn.jsdelivr.net/gh/mapleobserver/wsmud-script
@js ($version) = $.ajax({type: "GET", url: "(repourl)/version.txt", async: false, cache: false}).responseText;
[if] (version) != null
  @js ($remoteVer) = var v = (version);v.(flowCode)
[if] (localVer) < (remoteVer) && (remoteVer) != null
  [if] (UPnum) == null
    ($UPnum) = 0
  [if] (UPnum) < 2
    [if] (UPnum) == 1
      @print 未获取到最新版本，使用备用地址再次尝试...
      ($repourl) = http://wsmud-cdn.if404.com
    ($UPnum) = (UPnum) + 1
    [if] (flowPath) != 根文件夹 && (flowPath) != null
      ($flowSource) = (repourl)/(flowPath)/(flowName).flow.txt
    [else]
      ($flowSource) = (repourl)/(flowName).flow.txt
    [if] (flowFinder) == null
      ($flowFinder) = 根文件夹
    @js WG.SendCmd("tm 当前(flowName)版本【(localVer)】，将开始自动更新至最新【(remoteVer)】。")
    ($f_ss)={"name":"(flowName)","source":"(flowSource)","finder":"(flowFinder)"}
    @js var time = Date.parse( new Date());var f=(f_ss);var n=f["name"];var s=f["source"];var fd=f["finder"];WorkflowConfig.removeWorkflow({"name":n,"type":"flow","finder":fd});$.get(s,{stamp:time},function(data,status){WorkflowConfig.createWorkflow(n,data,fd);});
    @wait 1500
    ($log) = ✅(flowName)已更新至最新版【(remoteVer)】，即将自动运行。
  [else]
    ($log) = ⭕(flowName)暂时无法正常更新至【(remoteVer)】，直接运行当前版本【(localVer)】。
    ($UPnum) = null
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>
  [if] (UPnum) != null
    @js var f=(f_ss);ManagedPerformerCenter.start(f["name"], FlowStore.get(f["name"]));
    [exit]
[else]
  ($UPnum) = null
  [if] (remoteVer) == null
    ($log) = ⚠️获取远程版本失败，维持当前版本不变。
  [else]
    ($log) = 🚀开始运行(flowName)，当前版本【(remoteVer)】。更新内容：(changeLog)
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>

@cmdDelay 500
@js alert("1、拿残页(告辞)跟拿武器(紫)功能不支持20-40岁游戏角色\n2、由于不灭被动和九烛回血的机制,不能被骑鹰老头昏迷,所以请不要使用有不灭被动或阴阳技能的内功进副本,可以换用明玉之类的内功,后面有选项可以设置打小楼使用的内功。\n3、本流程需前台使用,放后台易导致卡本及点选项延迟。")
[if] (TodayNum) != null
    [if] (:date) != (TodayDate)
        [if] (:hour) > 4
            ($TodayNum) = 0
            ($TodayDate) = (:date)
[else]
    ($TodayNum) = 0

[if] (TotalKills) == null
    ($TotalKills) = 0
[if] (force) == null
    ($force) = (:kf_nei)
[if] (YYForce) == null
    ($YYForce) = 不更换
($num) = 0
($repeat) = 1

#input ($TodayNum) = <ord>🙏 衷心建议先看几遍副本攻略再使用本流程。</ord><br/>&nbsp*&nbsp今天已完成次数(如果不清楚就不要改),(TodayNum)
#select ($status) = 确认状态，是否已装备刀剑、技能装备是否都调整好,已完成|未装备武器,(status)
#select ($method) = <hiz>超龄剧情可拿5残页或紫刀；20-40岁可正常结婚，否则选超龄；面板够可选杀小楼多拿残页；拿草就是只拿草。</hiz><br/>&nbsp*&nbsp选择剧情,超龄剧情拿残页|超龄剧情拿紫武器|正常结婚|超龄结婚|杀小楼|拿草,(method)
#select ($YYAfterAction) = 剧情结束后操作,杀小楼|出副本,(YYAfterAction)
#config

@stopSSAuto
stopstate
team out

[if] (status) != 已完成 || (status) == null || (method) == null
    @print 请检查参数设置
    [exit]
[if] (TodayNum) >= 10
    @print <ord>今天已完成 10 次，无法再进入！</ord>
    $zdwk
    [exit]
[if] (method) == 正常结婚 || (method) == 超龄结婚
    #select ($timecheck) = 是否开启月圆之夜计时器,是|否,(timecheck)
    #config
    [if] {b忘忧草#}? == null || {b忘忧草#}? < 4
        @print 背包内的忘忧草不足4棵 无法正常进行结婚剧情
        tm 背包内的忘忧草不足4棵 无法正常进行结婚剧情
        [exit]
    [if] {b醉仙酿g#}? == null || {b醉仙酿g#}? < 10
        @print 背包内的醉仙酿不足10壶 现在前往醉仙楼买酒
        tm 背包内的醉仙酿不足10壶 现在前往醉仙楼买酒
        [while] (:room) != 扬州城-醉仙楼
            $to 扬州城-醉仙楼
            @wait 200
        list {r店小二}
        @dialog
        buy 10 {d醉仙酿}? from {r店小二}

[else if] (method) == 杀小楼 || (method) == 拿草
    ($repeat) = 10 - (TodayNum)
    #input ($repeat) = 循环次数,(repeat)
    [if] (method) == 杀小楼
        #select ($YYForce) = 打小楼要更换的内功,自创|慈航|九阴|阴阳|不更换,(YYForce)
        #select ($YYProp) = 杀小楼满26次地窖要拿什么,拿弯刀残页|拿橙刀,(YYProp)
        #input ($TotalKills) = 已杀小楼次数(如果不清楚就不要改),(TotalKills)
    #config
    [if] {b金创药g#}? == null || {b金创药g#}? < 10
        tm 背包内的金创药不足10包 现在前往药铺买药
        [while] (:room) != 扬州城-药铺
            $to 扬州城-药铺
            @wait 200
        list {r平一指}
        @dialog
        buy 10 {d金创药}? from {r平一指}
    [if] (YYForce) == 自创
        ($yyforce) = enable force (:id)
    [if] (YYForce) == 慈航
        ($yyforce) = enable force cihangjiandian
    [if] (YYForce) == 九阴
        ($yyforce) = enable force jiuyinshengong
    [if] (YYForce) == 阴阳
        ($yyforce) = enable force yinyangjiuzhuan

[while] (num) < (repeat)
    [if] (:mpPer) < 0.3
      $to 扬州城-武庙
      @dazuo
      go east
    enable force none
    enable force (force)
    cr wuyue/qingcheng/shanlu
    @until (:room) == 青城山-青城山路(副本区域)
    go westup
    go north
    yywd ok
    pa tu
    @tip 神界就在前方，只要伸手就到了.....
    answer s2
    @tip 神界就在前方，不要管那么多了.....
    answer s4
    @tip 你是人是鬼？
    answer s5
    @tip 不要阻止
    answer s7
    @tip 我不是为了寻死，只是觉得有些诡异...
    answer s9
    @tip 你带我去看看吧...
    answer s10
    @tip 没有我带路你是走不进去的
    [while] true
        [if] (:room) == 忧愁谷-谷底
            [break]
        @tip 青青往($distance)方走去。|青青走的太快，你已经($lost)她的身影了
        [if] (lost) != null
            @print <ord>跟丢了，可能设备反应太慢或网络不稳定，建议换个时间再试。</ord>
            tm 跟丢了，可能设备反应太慢或网络不稳定，建议换个时间再试。
            ofb
            @tip 当天进入次数($yynum)/10
            ofb ok
            ($TodayNum) = (yynum)
            $zdwk
            [exit]
        [if] (distance) == 东
            go east
        [if] (distance) == 南
            go south
        [if] (distance) == 西
            go west
        [if] (distance) == 北
            go north
    @tip 瞬时间就消失在远方的黑暗中
    answer s1
    cai cao
    @tip 挡住
    [if] (method) == 拿草
        answer s4
    [else]
        answer s3
    @await 2000
    @until (:combating) == false
    @until (:status faint) == true || (:status faint) == false || (:living) == false
    // 被骑鹰老头打死
    [if] (:living) == false
        [while] (:living) == false
            relive
            @wait 100
        ($yynum) = null
        ($dead) = true
        [if] (method) != 拿草
            @renew
    // 没死且没被昏迷则判断打死了老头
    [else if] (:status faint) == false
        ($fail) = true
        go south
    [else]
        [if] (method) == 杀小楼
            @until (:room) == 忧愁谷-花厅
            go west;go north
            @until (:room) == 忧愁谷-小楼
            [if] (YYForce) != 不更换
                (yyforce)
            @cd
            use {b金创药}?
            kill {r仇小楼}
            @tip 从仇小楼的尸体|凝听后($dijiao)大变
            [if] (dijiao) != null
                ($dijiao) = null
                @until {r仇小楼}? == null
                [while] (:room) == 忧愁谷-小楼
                  go down
                  @await 200
                @tip 不放过
                @cd
                enable force none
                [if] (YYForce) != 不更换
                    (yyforce)
                [else]
                    enable force (force)

                // 不放过
                // 进地窖后不选择直接叫杀避免小楼面板变高
                //answer1 {r仇小楼}
                kill {r仇小楼}

                @until {r仇小楼的尸体}? != null
                @tip 给你
                [if] (YYProp) == 拿弯刀残页
                    answer s2
                [else if] (YYProp) == 拿橙刀
                    answer s1
                    ($getblade) = true
                go up
                ($anew) = true
            @until (:room) == 忧愁谷-小楼
            @await 1000
            go south[2]
            go south[2]
            [if] (anew) == true
                ($anew) = false
                ($TotalKills) = 0
            [else]
                ($TotalKills) = (TotalKills) + 1
        [else]
            @tip 青青看了你一眼转身跟了过去。
            go west
            @print ***************************************
            @print ------------此处需等待10分钟-----------
            @print ***************************************
            ($waitnum) = 0
            [while] (waitnum) < 10
                @await 60000
                ($waitnum) = (waitnum) + 1
                tm 已等待 (waitnum) 分钟
            @print ***************************************
            @print ------------可以进入小楼了-------------
            @print ***************************************
            [if] (method) == 超龄剧情拿残页
                go north
                @tip 告辞
                answer s1
                @wait 1000
                [if] (YYAfterAction) == 杀小楼
                    [if] (YYForce) != 不更换
                        (yyforce)
                    use {b金创药}?
                    kill {r仇小楼}
                    @tip 从仇小楼的尸体|凝听后($dijiao)大变
                    [if] (dijiao) != null
                        ($dijiao) = null
                        @until {r仇小楼}? == null
                        [while] (:room) == 忧愁谷-小楼
                            go down
                            @await 200
                        @tip 不放过
                        @cd
                        enable force none
                        [if] (YYForce) != 不更换
                            (yyforce)
                        [else]
                            enable force (force)

                        // 不放过
                        // 进地窖后不选择直接叫杀避免小楼面板变高
                        //answer1 {r仇小楼}
                        kill {r仇小楼}

                        @until {r仇小楼的尸体}? != null
                        @tip 给你
                        [if] (YYProp) == 拿弯刀残页
                            answer s2
                        [else if] (YYProp) == 拿橙刀
                            answer s1
                            ($getblade) = true
                        go up
                        ($anew) = true
                @until (:room) == 忧愁谷-小楼
                @await 1000
                go south[2]
                go south[2]
                [if] (anew) == true
                    ($anew) = false
                    ($TotalKills) = 0
                [else]
                    ($TotalKills) = (TotalKills) + 1
            [if] (method) == 超龄剧情拿紫武器
                go north
                @tip 青青呢
                answer s2
                go south[2]
                @tip 后会有期
                answer s1
                @until (:room) == 忧愁谷-山洞
            [if] (method) == 正常结婚 || (method) == 超龄结婚
                go north
                [if] (method) == 超龄结婚
                    go south
                    go north
                @tip 我答应你
                answer s3
                @tip 月圆之夜就是你和青青拜堂之时
                go south
                go south
                @tip 我要带你出去
                answer s3
                go north
                go east
                @tip 我去找
                answer s1
                @tip 我才有把握让他们醉倒
                zxn
                @print ***************************************
                @print  结婚线
                @print  答应后向下走两步找青青
                @print  选离开可以拿武器
                @print  私奔需要5忘忧草和10醉仙酿
                @print
                @print ***************************************
                [if] (timecheck) == 是
                    @tip 你给了青青十壶醉仙酿和五株忘忧草。
                    ($timestep) = (:hour) * 3600 + (:minute) * 60 + (:second)
                    ($timefinish) = (timestep) + 3600 * 5
                    ($step) = 60
                    ($minutestep) = (step) / 60
                    ($minuterest) = 300
                    @print  ***************************************
                    @print  ---------------计时开始---------------
                    @print  ***************************************
                    [while] true
                        @cmdDelay 0
                        @await 6000
                        ($timenow) = (:hour) * 3600 + (:minute) * 60 + (:second)
                        ($timepast) = (timenow) - (timestep)
                        [if] (timepast) > (step)
                            ($minuterest) = (minuterest) - (minutestep)
                            ($timestep) = (timestep) + (step)
                            tm 月圆之夜倒计时(minuterest)分
                        [if](timenow) > (timefinish)
                            @cmdDelay 500
                            [break]
                    @print  ***************************************
                    @print  ---------------计时结束---------------
                    @print  ***************************************
                    @print  ***************************************
                    @print  时间到后向左向右一下就行
                    @print  <hiy>青青会过几分钟再回来</hiy>
                    @print  <hiy>请耐心等待</hiy>
                    @print  ***************************************
                    stopstate
                    go west
                    go east
                    @tip 他们都喝醉了，我们走吧
                    @print  ***************************************
                    @print  青青回来后会进入队伍
                    @print  前往谷口
                    @print  ***************************************
                    go west;go south
                    @until (:room) == 忧愁谷-谷口
                    @print  ***************************************
                    @print  选择保护青青
                    @print  获得青青契约
                    @print  ***************************************
                    @tip 保护青青
                    answer s8
                    @tip 恭喜你获得了青青的追随
                    go south
                    go south
    // 出副本
    //[if] (method) != 拿草 && (dead) == null
    [if] (dead) == null
        @await 1000
        ofb
        @tip 当天进入次数($yynum)/10
        ofb ok

    // 当天次数记录
    ($TodayDate) = (:date)
    [if] (yynum) != null
        ($TodayNum) = (yynum)
    [else]
        [if] (TodayNum) == null
            ($TodayNum) = 1
        [else]
            ($TodayNum) = (TodayNum) + 1
    [(fail)==true]($info) = 砍死了骑鹰老头，本次副本失败！
    [(dead) == true && (method) != 拿草]($info) = 被骑鹰老头打死，本次副本失败！
    [if] (info) != null
        ($info1) = (info)今日已完成 (TodayNum) 次。
        ($info2) = (info)总击杀楼哥 (TotalKills) 次。
    [else]
        ($info1) = 今日已完成 (TodayNum) 次。
        ($info2) = 总击杀楼哥 (TotalKills) 次。
    [if] (getblade) == true
        ($info3) = 橙刀已到手，终止流程。
        @print <hio>(info3)</hio>
        tm (info3)
    @print <ord>(info1)</ord>
    @print <ord>(info2)</ord>
    tm (info1)
    tm (info2)
    ($num) = (num) + 1
    [if] (TodayNum) > 9 || (getblade) == true
        [break]
@js ($yyfb)='(:room)'.indexOf('忧愁谷')
[if] (yyfb) == -1
    @renew
    $zdwk
    @recoverSSAuto