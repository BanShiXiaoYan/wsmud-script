// 白三三
// 版本检测
// 2021-10-03 16:42
($localVer) = 2021100304
($changeLog) = 📝 可以从古大陆或移花宫进入。\n🚌 目前路线有：掌门扳指、练功服、武道书、门派红残页、掌门令牌（探龙偷掌门）、打妖王、随从小师妹、购买物资。\n🛒 可以做路线任务顺便购买物资，也可以只购买不做其它事情。\n‼️ 建议先多脱几个技能避免比试打赢大师兄。\n🌺 没有宗门指环会自动改从移花宫进入。\n🔧 自动检测移花宫装备，秘籍默认真言手印，可以自己改。\n⚔️ 探龙偷掌门需要5500级探龙手，打妖王请设置更换装备技能的命令。\n🙏 如果有新攻略或者遇到bug，请到仙界群反馈。
($flowName) = 古代宗门
($flowCode) = gdzm
($flowPath) = 副本
($flowFinder) = 根文件夹
//($repourl) = https://cdn.jsdelivr.net/gh/mapleobserver/wsmud-script
($repourl) = http://wsmud-cdn.if404.com
@js ($version) = $.ajax({type: "GET", url: "(repourl)/version.txt", async: false, cache: false}).responseText;
[if] (version) != null
  @js ($remoteVer) = var v = (version);v.(flowCode)
[if] (localVer) < (remoteVer) && (remoteVer) != null
  [if] (UPnum) == null
    ($UPnum) = 0
  [if] (UPnum) < 2
    [if] (UPnum) == 1
      @print 未获取到最新版本，使用备用地址再次尝试...
      ($repourl) = http://wsmud-cdn.if404.com
    ($UPnum) = (UPnum) + 1
    [if] (flowPath) != 根文件夹 && (flowPath) != null
      ($flowSource) = (repourl)/(flowPath)/(flowName).flow.txt
    [else]
      ($flowSource) = (repourl)/(flowName).flow.txt
    [if] (flowFinder) == null
      ($flowFinder) = 根文件夹
    @js WG.SendCmd("tm 当前(flowName)版本【(localVer)】，将开始自动更新至最新【(remoteVer)】。")
    ($f_ss)={"name":"(flowName)","source":"(flowSource)","finder":"(flowFinder)"}
    @js var time = Date.parse( new Date());var f=(f_ss);var n=f["name"];var s=f["source"];var fd=f["finder"];WorkflowConfig.removeWorkflow({"name":n,"type":"flow","finder":fd});$.get(s,{stamp:time},function(data,status){WorkflowConfig.createWorkflow(n,data,fd);});
    @wait 1500
    ($log) = ✅(flowName)已更新至最新版【(remoteVer)】，即将自动运行。
  [else]
    ($log) = ⭕(flowName)暂时无法正常更新至【(remoteVer)】，直接运行当前版本【(localVer)】。
    ($UPnum) = null
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>
  [if] (UPnum) != null
    @js var f=(f_ss);ManagedPerformerCenter.start(f["name"], FlowStore.get(f["name"]));
    [exit]
[else]
  ($UPnum) = null
  [if] (remoteVer) == null
    ($log) = ⚠️获取远程版本失败，维持当前版本不变。
  [else]
    ($log) = 🚀开始运行(flowName)，当前版本【(remoteVer)】。更新内容：(changeLog)
  //@js WG.SendCmd("tm (log)")
  //@print <ord>(log)</ord>
// 预处理
@cmdDelay 500
@js alert('(changeLog)')
@js messageClear();messageAppend('(changeLog)',1,0)

// 检查移花宫装备
[if] {b★★★★★★邀月的手镯}? != null
  ($yihua) = 邀月的手镯
[else if] {b★★★★★★涟星的冰玉簪}? != null
  ($yihua) = 涟星的冰玉簪

// 预设进入方式
[if] (GZMway) == null
  [if] (:grade) == 武神 || (:grade) == 剑神 || (:grade) == 刀皇 || (:grade) == 兵主 || (:grade) == 战神
    ($GZMway) = 古大陆
  [else]
    ($GZMway) = 移花宫
[if] {b宗门指环}? == null && {b掌门扳指o}? == null && {b长老指环p}? == null
  ($GZMway) = 移花宫

// 检查秘籍
[if] {b真言手印秘籍}? != null
  ($book) = 真言手印秘籍
[else]
  ($book) = 手动填写

// 菜单
[(MHTriggers)==null]($MHTriggers)=血刀九阴,倚天九阴,血剑起手,混沌剑心,混沌九阴
#select ($GZMway) = ⛩ 进入古代宗门方式,古大陆|移花宫,(GZMway)
#select ($type) = <ord>所有路线奖励（除了购买物资）都是一次性的，一轮活动中重复进行不会再给。</ord><br/>&nbsp*&nbsp🚌 选择路线,掌门扳指|练功服|武道书|门派红残页|掌门令牌（探龙偷掌门）|掌门令牌（师妹方式）|打妖王|随从小师妹|购买物资,(type)
#input ($book) = 📚 给大长老的秘籍名字,(book)
#select ($yihua) = 💍 掌门扳指和练功服路线要给小师妹的物品,邀月的手镯|涟星的冰玉簪,(yihua)
#input ($MHTriggers) = ❌ 关闭战斗起手触发(多个触发名字用英文逗号隔开),(MHTriggers)
#select ($shopping) = <hiz>如果只想买东西不做其它，请选择路线【购买物资】</hiz><br/>&nbsp*&nbsp🛒 顺路购买物资,不买|只买玄晶|只买元晶|全都要,不买
#config

// 检查宗门指环
[if] {b宗门指环}? == null && {b掌门扳指o}? == null && {b长老指环p}? == null && (GZMway) == 古大陆
  ($GZMway) = 移花宫
  @print <ord>没有<hig>宗门指环</hig>，进入方式改为移花宫。</ord>
// 检查进入方式
[if] (GZMway) == null
  ($ready) =false
  @print <ord>没有选择进入古代宗门的方式，流程终止。</ord>
// 检查打妖王、随从小师妹、掌门令牌（师妹方式）路线是否有令牌
[if] (type) == 打妖王 || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
  [if] {b掌门令牌o}? == null
    ($lingpai) = false
  [else]
    ($lingpai) = true
  [if] (lingpai) == false && (type) == 随从小师妹
    ($ready) = false
    @print <ord>没有掌门令牌，无法进行随从小师妹路线，流程终止。</ord>
  [if] (lingpai) == true && (type) == 掌门令牌（师妹方式）
    ($ready) = false
    @print <ord>掌门令牌（师妹方式）路线中，背包不能有令牌，请选择其它路线用掉令牌或存仓，流程终止。</ord>
// 检查秘籍
[if] (lingpai) == false || (type) == 掌门扳指 || (type) == 练功服 || (type) == 武道书 || (type) == 门派红残页 || (type) == 掌门令牌（探龙偷掌门） || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
  @js ($miji) = '(book)'.indexOf('秘籍')
  [if] (miji) == -1
    ($book) = (book)秘籍
  [if] {b(book)o}? == null
    ($ready) = false
    @print <ord>没有<hio>(book)</hio>（<hiy>重新准备时请自己确保已学习该秘籍并且有备份，或者用手抄本</hiy>），流程终止。</ord>
// 检查移花宫装备
[if] (type) == 掌门扳指 || (type) == 练功服 || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
  [if] (yihua) != null && {b★★★★★★(yihua)y}? == null
    ($ready) = false
    @print <ord>没有<hiy>★★★★★★(yihua)</hiy>，流程终止。</ord>
// 掌门令牌（探龙偷掌门）路线检查是否有掌门扳指
[if] (type) == 掌门令牌（探龙偷掌门） || (type) == 打妖王
  [if] {b掌门扳指o}? == null
    ($eqjz) = (:eq5)
    stopstate;uneq (:eq5)
    [if] {b掌门扳指o}? == null
      ($ready) = false
      @print <ord>没有<hio>掌门扳指</hio>，流程终止。</ord>
// 购买物资路线检查是否选择购买内容
[if] (type) == 购买物资 && (shopping) == 不买
  ($ready) = false
  @print <ord>购买物资未选择购买物品，流程终止。</ord>
// 打妖王路线二级菜单
[if] (type) == 打妖王
  #select ($yaowang) = <ord>如果已经通过其它路线获得橙装，奖励物品将变成紫色品质。</ord><br/>&nbsp*&nbsp🏆 打死妖王后奖励物品三选一,练功服|元晶|指环,元晶
  #input ($GZMbosseq) = ⚔️ 打妖王前更换装备技能(用英文;隔开),(GZMbosseq)
  #config
  // 检查打妖王前更换装备技能命令
  [if] (GZMbosseq) == null
    ($ready) = false
    @print <ord>没有设置打妖王前更换装备技能的命令，流程终止。</ord>

// 未准备完善则终止。
[if] (ready) == false
  $zdwk
  [exit]

tm 本次副本路线：(type)
@print 🚌 本次副本路线：<hio>(type)</hio>
stopstate
team out
@renew

// 古大陆
[if] (GZMway) == 古大陆
  jh fam 9 start
  go enter
  go up
  @tip 打败我，你就($pass)上去|聚魂成功|踏过长生门|你已堪破生死|古老的大陆寻找真相|你连($pass)都没聚合|你想($pass)为神吗
  [if] (pass) != null
    @print <ord>不符合前往古大陆要求，流程终止。</ord>
    $zdwk
    [exit]
  ggdl {r疯癫的老头}
  go north[3]
  go north[3]
  look shi
  tiao1 shi;tiao3 shi
  tiao1 shi;tiao3 shi
  tiao2 shi
  go north

// 移花宫
[else if] (GZMway) == 移花宫
  @cmdDelay 1500
  jh fb 22 start2;cr huashan/yihua/shandao 1 0
  [while] (xuanwo) != true
    <---
    [if] {r神秘的尸体}? != null
      [if] {b宗门指环}? == null && {b掌门扳指o}? == null && {b长老指环p}? == null
        get all from {r神秘的尸体}?
      ($xuanwo) = true
      @cmdDelay 500
      [break]
    --->
    go south[5]
    go south[5]
    go south[5]
    $killall
    go south
    go south
    $killall
    go south
    $killall
    go southeast
    $killall
    @until (:combating) == false
    go northwest;go southwest
    $killall
    @until (:combating) == false
    look hua
    @tip 你数了下大概有($number)朵花
    go southeast
    look bed;pushstart bed;pushleft bed[(number)]
    @await 2000
    pushright bed[8]
    @await 2000
    go down
    fire;go west
    $killall
    @until (:combating) == false
    go west
    go west
    go north
    go north
    go north
    go north
    @print <ord>未找到尸体，请重试。</ord>
    cr;cr over
    $zdwk
    [exit]

look xuanwo
[if] (GZMway) == 古大陆
  tiao xuanwo
[else if] (GZMway) == 移花宫
  tiao xuanwo ok
go north

//进入副本
cr gumen/shanmen
go west
// 护山大阵
[{b掌门扳指o}? != null]($ring) = {b掌门扳指o}?
[{b长老指环p}? != null]($ring) = {b长老指环p}?
[{b宗门指环}? != null]($ring) = {b宗门指环}?
packitem tzdl (ring)
@tip 你却从明暗交错的光影中看到($distance)面似乎可以走出去
[if] (distance) == 东
  go east
[else if] (distance) == 南
  go south
[else if] (distance) == 西
  go west
[else if] (distance) == 北
  go north
// 进入宗门
go west[2]
go west[2]
[if] (shopping) != 不买 || (type) == 购买物资
  go north
  list {r长老}?
  [if] (shopping) == 只买玄晶 || (shopping) == 全都要
    buy 10000 {d玄晶}? from {r长老}?
  [if] (shopping) == 只买元晶 || (shopping) == 全都要
    buy 1 {d元晶}? from {r长老}?
    buy 1 {d元晶}? from {r长老}?
    buy 1 {d元晶}? from {r长老}?
    buy 1 {d元晶}? from {r长老}?
    buy 1 {d元晶}? from {r长老}?
  @await 3000
  go south
// 掌门扳指、练功服、武道书、门派红残页、掌门令牌（探龙偷掌门）、掌门令牌（师妹方式）、打妖王、随从小师妹
[if] (type) == 掌门扳指 || (type) == 练功服 || (type) == 武道书 || (type) == 门派红残页 || (type) == 掌门令牌（探龙偷掌门） || (type) == 打妖王 || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
  // 关闭自动出招和所有起手触发
  $stoppfm
  setting auto_pfm 0
  enable force none;enable sword none
  enable unarmed none;uneq (:eq0)
  [if] (MHTriggers) != null
    @js ($tgs)=var ts=("\""+"(MHTriggers)"+"\"").replace(/，/g,",");ts.replace(/,/g,"\",\"").split(",")
    @js ($tl)=[(tgs)].length
    ($num)=0
    [while] (num)<(tl)
      @js ($t)=[(tgs)][(num)]
      @js ToRaid.perform(`//~silent\n@off (t)`);
      ($num)=(num)+1
    @print 已关闭指定触发：(MHTriggers)
  // 比试大师兄
  @liaoshang
  fight {r大师兄}?
  @tip 可以告诉我一些宗门的事情吗
  answer {r大师兄}? 2
  @until (:combating) == false && (:free) == true && (:status faint) == false && (:status miss) == false && (:status tysp) == false
  @await 5000
  go east;go north
  @print 前往藏经阁找大长老
  @tip 大师兄托我来交还秘籍
  answer {r大长老}? 2
  give {r大长老}? 1 {b(book)}?
  @tip 这本书不像是从这里借出的
  @print ***************************************
  @print (:hour):(:minute):(:second) 此处需等待10分钟
  @print ***************************************
  @wait 600000
  @print ***************************************
  @print 可以去找大师兄了
  @print ***************************************
  stopstate
  go south;go west
  @tip 大长老喊你过去
  answer {r大师兄}? 1
  go east;go north
  @tip 这些东西应该对你有用
  // 大长老三选一，涉及路线：掌门扳指、练功服、掌门令牌（探龙偷掌门）、掌门令牌（师妹方式）、打妖王、随从小师妹
  [if] (type) == 掌门扳指 || (type) == 练功服 || (type) == 掌门令牌（探龙偷掌门） || (type) == 打妖王 || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
    answer {r大长老}? 1
    go south;go west
    @tip 把秘籍给他
    answer {r大师兄}? 1
    @tip 下次带你去个好玩的地方
    @print ***************************************
    @print (:hour):(:minute):(:second) 此处需等待10分钟，直到大师兄找你对话
    @print ***************************************
    @wait 600000
    @print ***************************************
    @print 可以去找大师兄了
    @print ***************************************
    stopstate
    go north;go south
    @tip 带你去个好地方
    // 涉及路线：掌门扳指、练功服、随从、掌门令牌（师妹方式）
    [if] (type) == 掌门扳指 || (type) == 练功服 || (type) == 随从小师妹 || (type) == 掌门令牌（师妹方式）
      answer {r大师兄}? 2
      //@until (:room) == 上古宗门(万年前)-练武场(副本区域)
      go south
      @tip 你就是最近和大师兄打的火热
      answer {r小师妹}? 2
      give {r小师妹}? 1 {b★★★★★★(yihua)}?
      @print ***************************************
      @print (:hour):(:minute):(:second) 此处需等待10分钟，直到小师妹找你对话
      @print ***************************************
      @wait 600000
      @print ***************************************
      @print 可以去找小师妹了
      @print ***************************************
      stopstate
      go north;go south
      @tip 带你去个好玩的地方
      [if] (type) == 掌门扳指 || (type) == 掌门令牌（师妹方式）|| (type) == 随从小师妹
        answer {r小师妹}? 1
        @tip 你们来到一个瀑布下面
        look teng
        open teng
        @tip 就是这个雕像
        look hu
        fl hu
        [if] (type) == 掌门扳指 || (type) ==掌门令牌（师妹方式）
          @tip 我们去别的地方玩吧
          answer {r小师妹}? 2
          @tip 好不容易找个保镖
          @tip 区区一个令牌
          [if] (type) == 掌门扳指
            look teng
            open teng
            look hu
            fl hu
            @tip 岩壁露出一个黑幽幽的洞口
            go down
            @tip 你就是大长老所说的外来人
            answer {r掌门}? 1
            @tip 这件器物就拜托你代为保管
            get all from {r妖王的尸体}?
            @print <hiy>掌门扳指路线已完成，流程即将结束。</hiy>
          [else if] (type) == 掌门令牌（师妹方式）
            @print <hiy>掌门令牌（师妹方式）路线已完成，流程即将结束。</hiy>
        [else if] (type) == 随从小师妹
          @tip 有股不详的预感
          go down
          @tip 你就是大长老所说的外来人
          answer {r掌门}? 1
          @tip 能不能拜托你照顾小女
          answer {r掌门}? 1
          @tip 恭喜你获得了小师妹的追随
          get all from {r妖王的尸体}?
          @print <hiy>随从小师妹路线已完成，流程即将结束。</hiy>
      [else if] (type) == 练功服
        answer {r小师妹}? 2
        @tip 这是我找大长老求了很久才给我的
        @print <hiy>练功服路线已完成，流程即将结束。</hiy>

    // 主线，和大师兄去悬崖，涉及路线：掌门令牌（探龙偷掌门）、打妖王
    [else if] (type) == 掌门令牌（探龙偷掌门） || (type) == 打妖王
      answer {r大师兄}? 1
      @tip 这条小路只有我知道
      answer {r大师兄}? 1
      @tip 这个瀑布别看不大
      answer {r大师兄}? 1
      @tip 遥想万年后却化为一片废墟
      answer {r大师兄}? 2
      @tip 平原上有一座繁华的神殿
      answer {r大师兄}? 2
      @tip 你赶紧拉着还在张大嘴巴的震撼中的大师兄从原路返回
      @until (:room) == 上古宗门(万年前)-练武场(副本区域)
      eq {b掌门扳指o}?
      go west
      @tip 你愿意帮我个忙吗
      answer {r大长老}? 1
      @tip 你可以自行做决定
      go east
      @tip 我去帮大长老做事
      go west
      go west
      @tip 我只是来看看
      answer {r掌门}? 2
      // 换装备技能
      enable unarmed feilongshou
      ($tlnum) = 0
      [while] (tlnum) < 3
        @cd unarmed.luo
        @perform unarmed.luo
        @await 500
        ($tlnum) = (tlnum) + 1
      [if] {b掌门令牌o}? == null
        @print 未偷到掌门令牌，请手动尝试或检查探龙手是否等级过低。
        @print 等待手动偷令牌或离开副本……
        @until {b掌门令牌o}? != null || (:room 副本区域) == false
      // 主线，打妖王
      [if] (type) == 打妖王
        go west
        go southeast
        // 换打妖王的装备技能
        @liaoshang
        (GZMbosseq)
        @await 6000
        look teng;open teng
        look hu;fl hu
        @tip 岩壁露出一个黑幽幽的洞口
        go down
        @tip 终于想要结束了吗
        answer {r妖王}? 1
        @until (:combating) == true
        @until (:combating) == false || (:living) == false
        [if] (:living) == false
          @print 打不过妖王，请复活重新配置装备技能，或者结束副本。
          @beep
        [else]
          @tip 这里有几样东西也许对你有用
          // 三选一，拿元晶，主线100%
          answer {r掌门}? 2
  // 大长老三选一，涉及路线：武道书、门派红残页
  [else if] (type) == 武道书
    answer {r大长老}? 3
    @print <hiy>武道书路线已完成，流程即将结束。</hiy>
  [else if] (type) == 门派红残页
    answer {r大长老}? 2
    @print <hiy>门派红残页路线已完成，流程即将结束。</hiy>
// 结束副本
cr;cr over
$zdwk